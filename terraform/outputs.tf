# Console output of cluster IPs
output "cluster_ips" {
  value = concat(
    [
      for w in oci_core_instance.worker :
      "${w.display_name}: ${w.public_ip}"
    ],
    [
      for m in oci_core_instance.master :
      "${m.display_name}: ${m.public_ip}"
    ]
  )
}

# Generation of ../ansible/inventory file
resource "local_file" "cluster_ips" {
  content = <<-DOC
  # Ansible inventory containing variable values from Terraform.
  # Generated by Terraform configuration during terraform apply

  [workers]
  %{for idx, w in oci_core_instance.worker~}
  k8-worker-${idx + 1} ansible_host=${w.public_ip} kube_name=k8-worker-${idx + 1} private_ip=${oci_core_instance.worker[idx].private_ip}
  %{endfor}

  [master]
  %{for idx, m in oci_core_instance.master~}
  k8-master-${idx + 1} ansible_host=${m.public_ip} kube_name=k8-master-${idx + 1} private_ip=${oci_core_instance.master[idx].private_ip}
  %{endfor}
  DOC

  filename   = "../ansible/inventory"
  depends_on = [oci_core_instance.worker, oci_core_instance.master]
}


output "notify_admin_about_inventory" {
  value      = "inventory file successfully generated to ../ansible/group_vars.yaml"
  depends_on = [local_file.cluster_ips]
}

output "cluster_ips" {
  value = concat(
    [
      for w in oci_core_instance.worker :
      "${w.display_name}: ${w.public_ip}"
    ],
    [
      for m in oci_core_instance.master :
      "${m.display_name}: ${m.public_ip}"
    ]
  )
}

resource "local_file" "cluster_ips" {
  content    = <<-DOC
  # Ansible inventory containing variable values from Terraform.
  # Generated by Terraform configuration during terraform apply
  [workers]
  %{for w in oci_core_instance.worker~}
  ${w.public_ip}
  %{endfor}
  [master]
  %{for m in oci_core_instance.master~}
  ${m.public_ip}
  %{endfor}
  DOC
  filename   = "../ansible/group_vars.yaml"
  depends_on = [oci_core_instance.worker, oci_core_instance.master]
}

output "notify_admin_about_inventory" {
  value      = "inventory file successfully generated to ../ansible/group_vars.yaml"
  depends_on = [local_file.cluster_ips]
}
